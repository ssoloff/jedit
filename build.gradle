plugins {
    id 'base'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.4'
    id 'de.undercouch.download' version '2.1.0'
}

group = 'io.github.ssoloff'

ext {
    downloadsDir = file("$buildDir/tmp/downloads")
    jeditDistUrl = "http://downloads.sourceforge.net/project/jedit/jedit/$version/jedit-$version-noarch-1sao.tgz"
    jeditDistArchive = file("$downloadsDir/jedit-${version}.tgz")
    jeditJar = file("$libsDir/jedit-${version}.jar")
}

task downloadJEdit() {
    outputs.file jeditJar

    doLast {
        download {
            src jeditDistUrl
            dest jeditDistArchive
        }

        copy {
            from tarTree(jeditDistArchive).files
            into libsDir
            include 'jedit.jar'
            rename { jeditJar.name }
        }
    }
}

bintrayUpload.dependsOn downloadJEdit

publishing {
    publications {
        maven(MavenPublication) {
            artifact jeditJar
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey

    publish = true
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = 'io.github.ssoloff:jedit'
        desc = 'jEdit application and plugin API'
        websiteUrl = 'https://github.com/ssoloff/jedit'
        issueTrackerUrl = 'https://github.com/ssoloff/jedit/issues'
        vcsUrl = 'https://github.com/ssoloff/jedit.git'
        licenses = ['GPL-2.0']
        publicDownloadNumbers = true

        version {
            name = "${project.version}"
            desc = "jEdit ${project.version}"
        }
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = '2.10'
}
