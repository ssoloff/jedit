import de.undercouch.gradle.tasks.download.Download

plugins {
    id 'base'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.5'
    id 'de.undercouch.download' version '2.1.0'
}

group = 'io.github.ssoloff'

ext {
    downloadsDir = file("$buildDir/tmp/downloads")
    jeditBinDistTar = file("$downloadsDir/jedit-$version-bin.tar.gz")
    jeditJar = file("$libsDir/jedit-${version}.jar")
}

task downloadJEditBinDistTar(type: Download) {
    src "http://downloads.sourceforge.net/project/jedit/jedit/$version/jedit-$version-noarch-1sao.tgz"
    dest jeditBinDistTar
    onlyIfNewer true
}

task prepareJEditJar(dependsOn: downloadJEditBinDistTar) {
    inputs.file jeditBinDistTar
    outputs.file jeditJar

    doLast {
        copy {
            from tarTree(jeditBinDistTar).files
            into libsDir
            include 'jedit.jar'
            rename { jeditJar.name }
        }
    }
}

task wrapper(type: Wrapper) {
    description = 'Generates Gradle wrapper files.'
    group = 'Build Setup'

    gradleVersion = '2.10'
}

bintrayUpload.dependsOn prepareJEditJar

publishing {
    publications {
        maven(MavenPublication) {
            artifact jeditJar
            artifact source: jeditBinDistTar, classifier: 'bin', extension: 'tar.gz'
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    dryRun = bintrayDryRun.toBoolean()
    publish = true
    publications = ['maven']

    pkg {
        repo = 'maven'
        name = 'io.github.ssoloff:jedit'
        desc = 'jEdit application and plugin API'
        websiteUrl = 'https://github.com/ssoloff/jedit'
        issueTrackerUrl = 'https://github.com/ssoloff/jedit/issues'
        vcsUrl = 'https://github.com/ssoloff/jedit.git'
        licenses = ['GPL-2.0']
        publicDownloadNumbers = true

        version {
            name = "${project.version}"
            desc = "jEdit ${project.version}"
        }
    }
}
